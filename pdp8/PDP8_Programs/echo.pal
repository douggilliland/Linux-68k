/ Serial port test (echo)
/ http://homepage.divms.uiowa.edu/~jones/pdp8/man/tty.html
*10
linep, 0
*20
saved, 0
count, 0
*400             / Line stored here
line, 0
*200
    CLA          / Clear Accumulator
    TLS          / Teleprinter Load and start
newl, CLA        / Clear Accumulator
    TAD (line-1)
    DCA linep
newc, KSF
    JMP .-1
    KRB          / Keyboard Read and begin next read
    AND (177)    / Get rid of parity bit
    TSF          / Teleprinter Skip if Flag
    JMP .-1
    TLS          / Teleprinter Load and start (Echo it)
    DCA saved    / save a copy
    TAD saved
    DCA I linep  / store it away
    TAD saved
    TAD (-15)    / CR character?
    SZA          / finished line if so - Skip next instruction if Zero Accumulator
    JMP newc
    TAD (12)     / Echo line feed as well
    TSF          / Teleprinter Skip if Flag
    JMP .-1
    TLS          / Teleprinter Load and start (Echo it)
/ Now echo the line
    CLA          / Clear Accumulator
    TAD (-line)
    TAD linep    / get number of characters
    CMA          / minus the number
    DCA count
    TAD (line-1) / reset starting address
    DCA linep
echoc, CLA       / Clear Accumulator
    TAD I linep  / get character
    TSF          / Teleprinter Skip if Flag
    JMP .-1
    TLS          / Teleprinter Load and start
    ISZ count    / Increment and store
    JMP echoc
    CLA          / Clear Accumulator
    TAD (12)
    TSF          / Teleprinter Skip if Flag
    JMP .-1
    TLS          / Teleprinter Load and start
    JMP newl
