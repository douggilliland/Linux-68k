/ Serial port test (echo)
*10
linep, 0
*20
saved, 0
count, 0
*400        / Line stored here
line, 0
*200
    CLA / Initialize
    TLS
newl, CLA
    TAD (line-1)
    DCA linep
newc, KSF
    JMP .-1
    KRB         / Get the character
    AND (177)   / Get rid of parity bit
    TSF
    JMP .-1
    TLS         / Echo it
    DCA saved   / save a copy
    TAD saved
    DCA I linep / store it away
    TAD saved
    TAD (-15)   / CR character?
    SZA         / finished line if so
    JMP newc
    TAD (12)    / Echo line feed as well
    TSF
    JMP .-1
    TLS
/ Now echo the line
    CLA
    TAD (-line)
    TAD linep   / get number of characters
    CMA         / minus the number
    DCA count
    TAD (line-1) / reset starting address
    DCA linep
echoc, CLA
    TAD I linep / get character
    TSF
    JMP .-1
    TLS
    ISZ count
    JMP echoc
    CLA
    TAD (12)
    TSF
    JMP .-1
    TLS
    JMP newl
