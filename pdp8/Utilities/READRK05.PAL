/ RK05 READ CHECK
/ Do a read check on all sectors.  Header words should be checked in all
/ sectors except 0.  0 probably missed since won't be first after seek
/ To run start at 0200 with SR9-11 with drive number to test
/ Program will halt on error, hit continue to go on to next sector.
/ If the switch register is not zero at the end of a pass the program
/ will halt
        DSKP=6741
        DCLR=6742
        DLAG=6743
        DLCA=6744
        DRST=6745
        DLDC=6746

        *20
DRIVE,  0
K0200,  0200
K4000,  4000
FLD,    0
SECT,   0
CYL,    0
MAXCYL, 4520
EXBIT,  0
REDDAT, 0000
CURENT, 3000
K17,    17

        *200
	CLA CLL OSR
	RAL
	DCA DRIVE
RESTRT,	CLA
        DCA SECT
        DCA CYL
        DCA EXBIT
RECAL,  CLA CLL IAC
        DCLR
        TAD DRIVE
        DLDC
        CLA CLL CML RTL
        DCLR
        DSKP
        JMP .-1
        TAD K0200
        TAD DRIVE
        DLDC
        DSKP
        JMP .-1
        DRST
        CIA
        TAD K4000
        SZA CLA
        JMP BADSTA
        JMP READ
BADSTA, HLT

READ,   CLA CLL IAC
        DCLR
        TAD CURENT
        DLCA
        TAD DRIVE
        TAD REDDAT
        TAD FLD
        TAD EXBIT
        DLDC
        TAD CYL
        TAD SECT
        DLAG
        TAD EXBIT
        RAR
        TAD CYL
        TAD SECT
        RAR
        DSKP
        JMP .-1
        DRST
        CIA
        TAD K4000
        SZA CLA
        JMP ERROR
NEXT,   CLA CLL IAC RAL
        TAD SECT
        AND K17
        DCA SECT
        CLA CMA
        TAD SECT
        SZA
        JMP CHK0
        CLA
        DCA SECT
        JMP INCCYL
CHK0,   IAC
        SNA
        ISZ SECT
        JMP READ

INCCYL, TAD EXBIT
        SZA
        JMP CHKCYL
INCTRK, CLA
        TAD K17
        IAC
        TAD CYL
        DCA CYL
        TAD CYL
        SZA
        JMP READ
        ISZ EXBIT
        JMP READ
        HLT
CHKCYL, CLA
        TAD CYL
        CIA
        TAD MAXCYL
        SZA
        JMP INCTRK
DONE,   CLA OSR
        SZA
        HLT
        JMP RESTRT
ERROR,  CLA
        DRST
        HLT
        JMP NEXT
        $
