/2 DF32 SYSTEM HANDLER
/
/
/
/
/
/
/
/
/
/COPYRIGHT  (C)  1974,1975 BY DIGITAL EQUIPMENT CORPORATION
/
/
/
/
/
/
/
/
/
/
/THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT NOTICE
/AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL EQUIPMENT
/CORPORATION.  DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY
/FOR ANY ERRORS THAT MAY APPEAR IN THIS DOCUMENT.
/
/THE SOFTWARE DESCRIBED IN THIS DOCUMENT IS FURNISHED TO THE PURCHASER
/UNDER A LICENSE FOR USE ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED
/(WITH INCLUSION OF DIGITAL'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH
/SYSTEM, EXCEPT AS MAY OTHERWISE BE PROVIDED IN WRITING BY DIGITAL.
/
/DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY FOR THE USE
/OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY
/DIGITAL.
/
/
/
/
/
/
/
/
/
/
/MAINTENANCE RELEASE CHANGES:

/1.	TOOK OUT SOFSET

DF32=1
	RF08=0
	VERSION="B&77

	*0
	-1
	DEVICE DF32;DEVICE SYS;4124;2007;0;177

	STARTB-ENDB-1

	NOPUNC
	*6604
	ENPUNC

STARTB,	NOP		/FOR "SWAP"
B6653,	6653
B7647,	7647
B7577,	7577
B200,	200
B7605,	7605
B7751,	7751
	ZBLOCK	6622-.
	TAD I B6653
	CDF 10
	DCA I B7647
	CDF 0
	ISZ B6653
	ISZ B7647
	JMP .-6		/MOVE FIELD 1 RESIDENT UP
	IFNZRO	RF08	<6643>
	6615
	7600
	TAD B7577
	DCA I B7751
	TAD B200
	6603		/NOW READ IN FIELD 0 RESIDENT FROM RECORD 1/2

	IFNZRO	RF08	<6623>
	IFNZRO	DF32	<6622>
	JMP .-1
	6621
	IFNZRO	RF08	<SKP>
	HLT		/ERROR READING SYSTEM IN
ENDB,	JMP I B7605
	/BOOTSTRAP FOR DISK MONITOR IS AS FOLLOWS:

	/	LOCATION	CONTENTS
	/	7750		7600
	/	7751		6603
	/	7752		6622
	/	7753		5352
	/	7754		5752
	*200

	NOPUNCH
	*7600
	ENPUNCH

	ZBLOCK 7
SHNDLR,	VERSION
	CLA CLL CMA RTL	/-3
	DCA SYSCNT		/# TRYS ON ERROR
	TAD I SHNDLR
	RAL
	CLA RTL
	TAD S6603
	DCA SFUN	/EITHER A READ OR WRITE
	TAD I SHNDLR
	AND S70
	DCA SFIELD	/GET FIELD OF BUFFER
	TAD I SHNDLR
	RAL
	AND S7600
	CIA
	DCA SWC		/SET UP WORD COUNT
	CLA CMA
	ISZ SHNDLR
	TAD I SHNDLR
	DCA SCA		/BUFFER ADDRESS-1
	ISZ SHNDLR
	IFNZRO	DF32 <
	TAD I SHNDLR
	RTL
	AND S3700>
	TAD SFIELD
	6615		/LOAD DISK EXTENDED MEMORY
S7600,	7600
	IFNZRO RF08 <
	TAD I SHNDLR
	RTR
	RTR
	AND S377
	6643		/LOAD HIGH ORDER>
	TAD I SHNDLR
	RTR
	RTR
	RAR
	AND S7400
SFUN,	HLT		/BECOMES DISK IOT.(READ OR WRITE)
	RDF
	TAD SCIF
	DCA SFIELD
	IFNZRO	DF32	<6622>
	IFNZRO	RF08	<6623>
	JMP .-1
	ISZ SHNDLR
	6621		/SKIP ON ERROR
	IFNZRO RF08 <SKP	/SENSE OF SKIP IS REVERSED>
	JMP SYSER
	ISZ SHNDLR
SFIELD,	HLT		/RETURN TO PROPER FIELD
	6601		/CLEAR TROUBLESOME FLAG
	JMP I SHNDLR
	ZBLOCK 2
SYSER,	CLA CLL CML RAR	/4000
	ISZ SYSCNT	/TRY AGAIN?
	SKP CLA
	JMP SFIELD	/WHY BOTHER
	CLA CLL CMA RTL
	TAD SHNDLR
	DCA SHNDLR	/RESET PARAMETERS AND TRY AGAIN
	IFNZRO RF08	<IFNZRO .-7700	<NZERR>;	SKP;	HLT>
	JMP SHNDLR+3
SCIF,	CIF 0
SYSCNT,	0
	IFNZRO DF32	<IFNZRO .-7700	<NZERR>;	SKP;	HLT>
S6603,	6603
S70,	70
S7400,	7400
	IFNZRO	DF32 <S3700,	3700>
	IFNZRO RF08 <S377,	377>
	SCA=7751
	SWC=7750
	$

