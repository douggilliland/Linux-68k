/2 LINCTAPE SYSTEM HANDLER
/
/
/
/
/
/
/
/
/
/COPYRIGHT  (C)  1974,1975,1977 BY DIGITAL EQUIPMENT CORPORATION
/
/
/
/
/
/
/
/
/
/
/THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT NOTICE
/AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL EQUIPMENT
/CORPORATION.  DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY
/FOR ANY ERRORS THAT MAY APPEAR IN THIS DOCUMENT.
/
/THE SOFTWARE DESCRIBED IN THIS DOCUMENT IS FURNISHED TO THE PURCHASER
/UNDER A LICENSE FOR USE ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED
/(WITH INCLUSION OF DIGITAL'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH
/SYSTEM, EXCEPT AS MAY OTHERWISE BE PROVIDED IN WRITING BY DIGITAL.
/
/DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY FOR THE USE
/OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY
/DIGITAL.
/
/
/
/
/
/
/
/
/
/
/	SOFSET=7747
	SBLOCK=7776
	LINC=6141
	AXO=1
	PDP=2
	TMA=23
	*0

	VERSION="B&77

	-2
	DEVICE LINC;DEVICE SYS;4171;2007;0;1341
	DEVICE LINC;DEVICE LTA0;4171;1007;0;1341

/V3D:	REMOVED 'SOFSET'
	STARTB-ENDB-1

	NOPUNCH
	*7600
	ENPUNCH

STARTB,	ZBLOCK 16
B4047,	4047
B7647,	7647
	1020;20;4		/IO PRESET
	1020;20;AXO	/LOAD SOME LINCTAPE FLAGS
	1020;7600;TMA	/LOAD MEMORY ADDRESS
	720;1		/READ RECORD 1
	PDP		/BACK TO PDP-8 MODE
	CLA
	TAD I B4047
	CDF 10
	DCA I B7647
	CDF 00
	ISZ B4047
	ISZ B7647
	JMP .-6
	JMP I .+1
ENDB,	7605



	/THE BOOTSTRAP FOR A LINCTAPE SYSTEM IS AS FOLLOWS:

	/LEFT SWITCHES=700,RIGHT SWITCHES=0
	/I-O PRESET IN LINC MODE AND PRESS "D0". TAPE WILL MOVE
	/WHEN TAPE STOPS PRESS "START 20".
	*200

	NOPUNCH;*7600;ENPUNCH

	ZBLOCK 7
SHNDLR,	VERSION
S7600,	7600
	RDF
	TAD SCIF
	DCA SXIT
	TAD I SHNDLR
	DCA SFUN	/FUNCTION
	ISZ SHNDLR
	TAD I SHNDLR
	DCA SADR	/BUFFER ADDRESS
	ISZ SHNDLR
	TAD I SHNDLR
/V3D	TAD SOFSET	/SOFSET=0
	NOP		/SAVE ROOM
	CLL RAL
	DCA SBLOK	/BLOCK NO.
	ISZ SHNDLR
	CLL CML RAR
	AND SFUN
	RTL
	RTL
	TAD S702	/GET FUNCTION;CREATE READ OR WRITE
	DCA SINST	/READ OR WRITE INSTRUCTION.
	TAD SFUN
	AND S70		/FIELD BITS TO AC 0-2
	TAD SADCDF
	DCA SADSET
	TAD SADSET
	AND S70
	RTL
	IAC RTL		/SET EXTENDED ADDRESS
	RTL
	LINC
	AXO
	PDP
	CLA
	TAD SFUN
	RAL
	AND S7600
	DCA SFUN
SADSET,	0
SLOOP,	CLA CLL CMA RTL
	DCA SERRCT	/NO. ERROR TRIES
STRY,	TAD SADR
	TAD S200
	DCA SADNXT
	TAD I SADNXT
	DCA SADSVC
	TAD SADR
	LINC
	TMA		/CORE ADDRESS TO CONTROL
SINST,	0		/READ OR WRITE
SBLOK,	0		/BLOCK NO. HERE
	PDP
	CMA		/CHECKSUM HERE. 7777=GOOD
	DCA SADSET
	TAD SADSVC
	DCA I SADNXT
	IFNZRO .-7700 <NZERR>
	SKP
	HLT
	TAD SADSET
	SZA CLA
	JMP SERR
SOK,	ISZ SBLOK
	TAD SADNXT
	DCA SADR
	TAD SFUN
	TAD S7600
	SNA		/ALL DONE?
	JMP SDONE	/YES
	DCA SFUN
	JMP SLOOP
SERR,	TAD SINST
	RTR;RTR
	SPA CLA		/WAS IT READ?
	JMP SOK		/NO..WRITE. CONTINUE
	ISZ SERRCT	/READ..RETRY IT?
	JMP STRY
	CLA CLL CML RAR	/DON'T BOTHER
	SKP
SDONE,	ISZ SHNDLR
SXIT,	0
	JMP I SHNDLR
SFUN,	0
SADR,	0
SERRCT,	0
S702,	702
S200,	200
S70,	70
SCIF,	CDF CIF 0
SADCDF,	CDF 0
SADNXT,	0
SADSVC,	0
	$

