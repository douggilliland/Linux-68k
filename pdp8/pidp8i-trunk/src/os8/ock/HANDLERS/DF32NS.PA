/1 DF32 NON SYSTEM HANDLER
/
/
/
/
/
/
/
/
/
/COPYRIGHT  (C)  1974,1975 BY DIGITAL EQUIPMENT CORPORATION
/
/
/
/
/
/
/
/
/
/
/THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT NOTICE
/AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL EQUIPMENT
/CORPORATION.  DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY
/FOR ANY ERRORS THAT MAY APPEAR IN THIS DOCUMENT.
/
/THE SOFTWARE DESCRIBED IN THIS DOCUMENT IS FURNISHED TO THE PURCHASER
/UNDER A LICENSE FOR USE ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED
/(WITH INCLUSION OF DIGITAL'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH
/SYSTEM, EXCEPT AS MAY OTHERWISE BE PROVIDED IN WRITING BY DIGITAL.
/
/DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY FOR THE USE
/OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY
/DIGITAL.
/
/
/
/
/
/
/
/
/
/

/	SR

	RF08=0	/CHANGE TO 1 FOR RF08 HANDLER

/THE NUMBER OF PLATTERS MUST EITHER BE SET AT ASSEMBLY TIME,
/OR MUST BE CHANGED VIA THE ALTER COMMAND IN BUILD

	*0

	-1
	IFNZRO RF08 <
	DEVICE RF;DEVICE RF;4064;RF&177;ZBLOCK 2
	>
	IFZERO RF08 <
	DEVICE DF;DEVICE DF;4124;DF&177;ZBLOCK 2
	>

	SCA=7751
	SWC=7750

	RFVERSION="A&77
	*200

SYSER,	CLA CLL CML RAR	/4000
	ISZ SYSCNT	/TRY AGAIN?
	SKP CLA
	JMP SFIELD	/WHY BOTHER
	CLA CLL CMA RTL
	TAD RF
	DCA RF		/RESET PARAMETERS AND TRY AGAIN
	JMP RETRY
SCIF,	CIF 0
SYSCNT,	0
S6603,	6603
S70,	70
S7400,	7400
	IFZERO RF08 <S3700,	3700>
	IFNZRO RF08 <S377,	0377>
T1,	0
T2,	0
	ZBLOCK 224-.
	IFNZRO .-224 <ADRERR,QQQQ>	/ENTRY PT MUST BE RELATIVE 24
DF,
RF,	RFVERSION
	CLA CLL CMA RTL	/-3
	DCA SYSCNT	/# TRYS ON ERROR
RETRY,	TAD I RF	/HANDLER RUNS IN USER'S DATA FIELD
	RAL
	CLA RTL
	TAD S6603
	DCA SFUN	/EITHER A READ OR WRITE
	TAD I RF
	AND S70
	DCA SFIELD	/GET FIELD OF BUFFER
	TAD I RF
	RAL
	AND S7600
	CIA
	DCA T1		/SET UP WORD COUNT
	CLA CMA
	ISZ RF
	TAD I RF
	DCA T2		/BUFFER ADDRESS-1
	ISZ RF
	RDF
	TAD (CDF 0
	DCA RESRDF
	CDF 0
	TAD T1
	DCA I (SWC
	TAD T2
	DCA I (SCA
RESRDF,	HLT		/RESTORE USER'S DATA FIELD
	IFZERO RF08 <
	TAD I RF
	RTL
	AND S3700
	>
	TAD SFIELD
	6615		/LOAD DISK EXTENDED MEMORY
S7600,	7600
	IFNZRO RF08 <
	TAD I RF
	RTR
	RTR
	AND S377
	6643		/LOAD HIGH ORDER
	>
	TAD I RF
	RTR
	RTR
	RAR
	AND S7400
SFUN,	HLT		/BECOMES DISK IOT.(READ OR WRITE)
	RDF
	TAD SCIF
	DCA SFIELD
	IFZERO	RF08	<6622>
	IFNZRO	RF08	<6623>
	JMP .-1
	KRS
	AND (177
	TAD (-3
	SNA CLA
	KSF
	JMP .+3
	CIF CDF 0	/RETURN TO OS/8 IF USER TYPED ^C
	JMP I S7600
	ISZ RF
	6621		/SKIP ON ERROR
	IFNZRO RF08 <SKP	/SENSE OF SKIP IS REVERSED>
	JMP SYSER
	ISZ RF
SFIELD,	HLT		/RETURN TO PROPER FIELD
	6601		/CLEAR TROUBLESOME FLAG
	JMP I RF
	$

