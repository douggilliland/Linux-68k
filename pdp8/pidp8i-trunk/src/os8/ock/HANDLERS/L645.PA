/1 ANALEX LINE PRINTER HANDLER
/
/
/
/
/
/
/
/
/
/COPYRIGHT  (C)  1974,1975 BY DIGITAL EQUIPMENT CORPORATION
/
/
/
/
/
/
/
/
/
/
/THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT NOTICE
/AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL EQUIPMENT
/CORPORATION.  DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY
/FOR ANY ERRORS THAT MAY APPEAR IN THIS DOCUMENT.
/
/THE SOFTWARE DESCRIBED IN THIS DOCUMENT IS FURNISHED TO THE PURCHASER
/UNDER A LICENSE FOR USE ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED
/(WITH INCLUSION OF DIGITAL'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH
/SYSTEM, EXCEPT AS MAY OTHERWISE BE PROVIDED IN WRITING BY DIGITAL.
/
/DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY FOR THE USE
/OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY
/DIGITAL.
/
/
/
/
/
/
/
/
/
/
	*0
	-1
DEVICE L645;DEVICE LPT;1040;0;ZBLOCK 2

/V3 FIXES:

/1.	ADDED VERSION NUMBER
/2.	ADDED PARITY ^C
/3.	ALLOWED ^C TO WORK EVEN IF LPT OFF
/	S.R.

	LPTVERSION="A&77

	*200
	/LINE PRINTER HANDLER FOR "OLD STYLE" LINE PRINTER
	/RECOGNIZES TAB,LINE FEED,VERTICAL TAB AND FORM FEED
	/IGNORES CARRIAGE RETURNS, TREATS ^N AS "CARRIAGE RETURN
	/ WITHOUT LINE FEED" CONTROL CHARACTER.
	LSE=6651
	LCF=6652
	LLB=6654
	LSD=6661
	LCB=6662
	LPR=6664
LPT,	LPTVERSION
LP7700,	7700
LPT11,	11	/FALLS THROUGH HARMLESSLY, CLEARING THE AC
LPT214,	RDF
	TAD LPTCIF
	DCA LPTXIT
	TAD I LPT
	AND LPT70
	TAD LP6201
	DCA LPTCDF
	CLA CLL CML RAR
	TAD I LPT	/LINK IS NOW 1 IF COMMAND WAS A "WRITE"
	AND LP7700
	CMA
	DCA LPTWC	/LPTWC=WORD COUNT/2
	ISZ LPT
	TAD I LPT
	DCA LPTCA
	ISZ LPT
	TAD I LPT
	ISZ LPT
	SNL
	JMP LPTERR
	SZA CLA
	JMP LPTCDF
	LCB
	JMS LPWAIT
	TAD LPT214
	JMS LPTPCH	/FORM FEED, INITIALIZES COUNT
LPTCDF,	HLT
	JMP LPT7
LPTLP,	TAD I LPTCA
	JMS LPTPCH
	TAD I LPTCA
	AND LP7400
	DCA LPTTMP
	ISZ LPTCA
LP7400,	7400
	TAD I LPTCA
	JMS LPTPCH
	TAD I LPTCA
	AND LP7400
	CLL RTR
	RTR
	TAD LPTTMP
	RTR
	RTR
	JMS LPTPCH
	ISZ LPTCA
LPT7,	7
	ISZ LPTWC
	JMP LPTLP
LPTRTN,	ISZ LPT
LPTXIT,	HLT
	JMP I LPT
LPTWC,	0
LPTCA,	0
LPTPCH,	0
	AND LPT177
	SZA
	TAD LPM140
	SMA
	JMP I LPTPCH
	TAD LPT106
	SNA
	JMP LPTCTZ
	TAD LPT13
	CLL
	TAD LPT6
	SZL SNA
	JMP LPTCTL
	TAD LPT11
	LLB
	JMP LPTKSF
LPWAIT,	-1
WEIGHT,	LSD
	JMP NOTDON
	LCF
	ISZ LPLPTR
	JMP I LPWAIT
	CLA IAC
LPTCTL,	SNA
	JMP LPTTAB
	TAD LPTTAD
	DCA LPTXXX
	TAD LP7607
	DCA LPLPTR
LPTXXX,	HLT
	SNA
	JMP .+3
	LPR
LPTKSF,	JMS LPWAIT
	JMP I LPTPCH
NOTDON,	KRS
	AND LPT177
	TAD LPM3
	SNA CLA
	KSF
	JMP WEIGHT
LPTCIF,	CDF CIF 0
	JMP I .+1
	7600
LPTCTZ,	TAD LPT214
	JMS LPTPCH
	JMP LPTRTN
LPT6,	6
LPTTAB,	TAD LPT40
	LLB
	JMS LPWAIT
	TAD LPLPTR
	AND LPT7
LPM140,	SZA CLA
	JMP LPTTAB
	JMP I LPTPCH
LPTERR,	CLA CLL CML RAR
	JMP LPTXIT
LPTTAD,	TAD .
LPT70,	70	/LF
LPT13,	13	/VT
LPT177,	177	/FF
	0	/CR
LPT40,	40	/CR, NO LF
LPM3,	-3
LPT106,	106
LP7607,	7607
LP6201,	CDF 0
LPTTMP,	0
LPLPTR,	0
	$

