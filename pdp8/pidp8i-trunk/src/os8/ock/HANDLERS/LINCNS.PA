/1 LINCTAPE HANDLER FOR BUILD
/
/
/
/
/
/
/
/
/
/COPYRIGHT  (C)  1974,1975 BY DIGITAL EQUIPMENT CORPORATION
/
/
/
/
/
/
/
/
/
/
/THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT NOTICE
/AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL EQUIPMENT
/CORPORATION.  DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY
/FOR ANY ERRORS THAT MAY APPEAR IN THIS DOCUMENT.
/
/THE SOFTWARE DESCRIBED IN THIS DOCUMENT IS FURNISHED TO THE PURCHASER
/UNDER A LICENSE FOR USE ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED
/(WITH INCLUSION OF DIGITAL'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH
/SYSTEM, EXCEPT AS MAY OTHERWISE BE PROVIDED IN WRITING BY DIGITAL.
/
/DIGITAL EQUIPMENT CORPORATION ASSUMES NO RESPONSIBILITY FOR THE USE
/OR RELIABILITY OF ITS SOFTWARE ON EQUIPMENT THAT IS NOT SUPPLIED BY
/DIGITAL.
/
/
/
/
/
/
/
/
/
/
	*0
	-10
DEVICE LNC;DEVICE LTA0;4170;10;ZBLOCK 2
DEVICE LNC;DEVICE LTA1;4170;11;ZBLOCK 2
DEVICE LNC;DEVICE LTA2;4170;12;ZBLOCK 2
DEVICE LNC;DEVICE LTA3;4170;13;ZBLOCK 2
DEVICE LNC;DEVICE LTA4;4170;14;ZBLOCK 2
DEVICE LNC;DEVICE LTA5;4170;15;ZBLOCK 2
DEVICE LNC;DEVICE LTA6;4170;16;ZBLOCK 2
DEVICE LNC;DEVICE LTA7;4170;17;ZBLOCK 2

	LINCVERSION="A&77

	*200
	/LINCTAPE HANDLER FOR PS/8 - CRUDE VERSION (WRITE OPERATION CHECKS
	/AFTER EACH BLOCK)
	/ALL 8 HANDLERS ARE IN THIS PAGE - SEE DECTAPE HANDLER FOR
	/DUMB COMMENT

	/PDP-12 OPCODES NEEDED
	LINC=	6141
	PDP=	0002
	CLR=	0011
	AXO=	0001
	TMA=	0023
	TAC=	0003
	STDI=	0436
	COM=	0017
	ROLI1=	0261
	ESF=	0004

LTM203,	-203
LTA,	0
LTA7,	7
LTA30,	30
LTA70,	70
LTA702,	702
LT3700,	3700
LTATMP,	LINCVERSION
DTA0,	ISZ LTANO
DTA1,	ISZ LTANO
DTA2,	ISZ LTANO
DTA3,	ISZ LTANO
DTA4,	ISZ LTANO
DTA5,	ISZ LTANO
DTA6,	ISZ LTANO
DTA7,	ISZ LTANO
LTA200,	200
	TAD LTANO
	CMA
	TAD LTATAD
	DCA LTANO
	CLA CLL CML RTR
	TAD LTANO
	DCA LTADCA
	RDF
	TAD LCDIF0
	DCA LTAXIT
LTANO,	0
	DCA LTA
	TAD LTAISZ
LTADCA,	0
	TAD I LTA
	DCA LTARG1
	ISZ LTA
	TAD I LTA
	DCA LTARG2
	ISZ LTA
	TAD I LTA
	CLL RAL	/MOVE ARGUMENTS TO WORK AREA
	DCA LTARG3
	ISZ LTA
	ISZ LTA
	TAD LTARG1	/GET CORE FIELD
	AND LTA70
	TAD LTCDF
	DCA LTSET
	TAD LTSET
	AND LTA70
	CLL RTL
	RTL		/MOVE TO BITS 0-2. CONTROLLER
	RTL		/WANTS THEM THERE
SLTARG3,DCA LTATMP
	TAD LTANO	/GET UNIT NO.
	AND LTA7
	CLL RAR
	TAD LTATMP
	TAD LTA30	/SET BIT 7 ON. EXTENDED ADD. MODE
	LINC
	AXO		/SEND DATA TO CONTROLLER
	PDP
LT7600,	7600
	DCA LTANO	/RESET UNIT NO.
	TAD LTARG1
	RTL
	AND LTA3
	CLL RTL
	TAD LTA702	/ADD TAPE INST; STORE IT
	DCA LTINST
LTALP,	TAD LTARG2	/CORE ADDRESS TO CONTROLLER
LTATAD,	TAD LTA200	/SAVE 129 TH WORD
	DCA LTATMP
LTSET,	0
	TAD I LTATMP
	DCA LTASVC	/SAVE LOC.
	TAD LTARG2
	LINC
	TMA
LTINST,	0		/TAPE INSTRUCTION HERE
LTARG3,	0		/BLOCK NO. HERE
LTAWLP,	PDP
	CLA
	TAD LTASVC	/RESTORE 129TH WORD
	DCA I LTATMP
	TAD LTA200	/ADD 200 FOR PARITY TTY
	KRS
	TAD LTM203	/TEST FOR ^C
	SNA CLA
	KSF		/IS FLAG UP?
	JMP NOTFUG	/EITHER NOT ^C OR NO FLAG
	TAD LTA30
	LINC
	ESF
	PDP
LT7700,	7700
	TAD LTASVC
	DCA I LTATMP
LCDIF0,	CDF CIF 0
	JMP I LT7600
NOTFUG,	LINC
	STDI
	COM
	ROLI1
LTA3,	TAC
	PDP
	SNL
	JMP LTAWLP
LTADUN,	CLL IAC
	CLA IAC RTL
	AND LTINST
	SNA CLA
	JMP LTALP
LTNERR,	TAD LTARG1
	AND LT3700
	TAD LT7700
	SNA		/ALL DONE?
	JMP LTAXIT
	DCA LTARG1	/NO.. SAVE COUNT
	TAD LTATMP
	DCA LTARG2
	ISZ LTARG3
	JMP LTALP
LTAXIT,	HLT
	JMP I LTA
LTAISZ,	ISZ LTANO
LTARG1,	0
LTARG2,	0
LTCDF,	CDF 0
LTASVC=LTADCA
$$$$$$$$

