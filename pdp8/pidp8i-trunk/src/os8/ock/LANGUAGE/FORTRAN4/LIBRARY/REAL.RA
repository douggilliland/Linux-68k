/
/   R E A L
/   - - - -
/
/   A I M A G
/   - - - - -
/
/   C O N J G
/   - - - - -
/
/ VERSION 5A 4-27-77  PT
/
/REAL,AIMAG - ENTER IN COMPLEX,EXIT IN REAL
/CONJG - ENTER + EXIT IN COMPLEX
/
	SECT	REAL
	JA	#REAL
	DPCHK
	TEXT	+REAL  +
REALXR,	SETX	XRREAL
	SETB	BPREAL
BPREAL,	F 0.0
XRREAL,	F 0.0
ARG,	F 0.0
	F 0.0
	ORG	10*3+BPREAL
	FNOP
	JA	REALXR
	0
REALRT,	JA	.
	BASE	0
#REAL,	SETX	XRREAL
	LDX	0,2
	LDX	1,0
COMM,	STARTD
	FLDA	10*3
	FSTA	REALRT
	FLDA	0
	SETB	BPREAL
	BASE	BPREAL
	LDX	1,1
	FSTA	BPREAL
	FLDA%	BPREAL,1
	FSTA	BPREAL
	STARTE
	FLDA%	BPREAL		/GET ARG
	FSTA	ARG
	JXN	REAM,0
	STARTF
	FLDA	ARG+3
	FNEG
	FSTA	ARG+3
	STARTE
	FLDA	ARG
	FSTA	#CAC
	JA	REALRT
REAM,	STARTF
CON,	FLDA	ARG,2
	JA	REALRT
	EXTERN	#CAC
/
	ENTRY	AIMAG
AIMAG,	SETX	XRREAL
	LDX	1,2
	LDX	1,0
	JA	COMM
/
	ENTRY	CONJG
CONJG,	SETX	XRREAL
	LDX	0,2
	LDX	0,0
	JA	COMM
