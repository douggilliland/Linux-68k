/DOUBLE PRECISION BINARY AND DIRECT ACCESS I/O
/FOR OS/8 FORTRAN
/
/ VERSION 5A 4-26-77 MH
/
/I/O CALLS ARE:
/	TRAP3 #RSVO	ALL SINGLE PRECISION I/O
/	TRAP3 #RSVO	ALL DOUBLE PRECISION FORMATTED I/O
/	JSR #RFDV	DOUBLE PRECISION BINARY + DIRECT ACCESS I/O
/			TREATED AS 2 SINGLE PRECISION FORMATTED JOBS
	SECT	#RFDV
	EXTERN	#RSVO
	BASE	0
	STARTE
	FSTA	FTEMP3		/SAVE 6 WDS FOR A WRITE
	STARTD
	FLDA	0		/RETURN ADDRESS
	FSTA	RFDVRT		/SAVE FOR EXIT
	STARTF
	FLDA	FTEMP3		/PASS 1ST 3 WDS FOR A WRITE
	TRAP3	#RSVO		/DO THE READ OR WRITE
	FSTA	FTEMP3		/SAVE 1ST 3 WDS FROM A READ
	FLDA	FTEMP3+3	/GET 2ND 3 WDS FOR A WRITE
	TRAP3	#RSVO		/DO THE READ OR WRITE
	FSTA	FTEMP3+3	/SAVE 2ND 3 WDS FROM A READ
	STARTE
	FLDA	FTEMP3		/GET ALL 6 WORDS FOR A READ
RFDVRT,	JA .

FTEMP3,	E 0.0
