50  REMARKABLY TRANSLATED TO OS8 BASIC BY KAY R. FISHER ...DEC
100 REM VALUES FOR 1974: SEE NOTES
110 REM
115 DIM M(12)
120 FOR I=1 TO 6\PRINT\NEXT I
125 REM 1974 STARTS ON TUESDAY.(0=SUN.,-1=MON.,-2=TUE...)
130 D=-2
140 S=0
150 REM: READ DAYS OF EACH MONTH
160 FOR N=0 TO 12 \ READ M(N) \ NEXT N
170 REM *****************************
180 FOR N=1 TO 12
190 PRINT \ PRINT \ S=S+M(N-1)
200 PRINT "**";S;TAB(7);
210 FOR I=1 TO 18 \ PRINT "*"; \ NEXT I
220 IF N=2 GOTO 240\IF N=3 GOTO 250\IF N=4 GOTO 260
221 IF N=5 GOTO 270\IF N=6 GOTO 280\IF N=7 GOTO 290
222 IF N=8 GOTO 300\ IF N=9 GOTO 310\ IF N=10 GOTO 320
223 IF N=11 GOTO 330\ IF N=12 GOTO 340
230 PRINT " JANUARY "; \GOTO 350
240 PRINT " FEBRUARY"; \GOTO 350
250 PRINT "  MARCH  "; \GOTO 350
260 PRINT "  APRIL  "; \GOTO 350
270 PRINT "   MAY  "; \GOTO 350
280 PRINT "   JUNE  "; \GOTO 350
290 PRINT "   JULY  "; \GOTO 350
300 PRINT "  AUGUST "; \GOTO 350
310 PRINT "SEPTEMBER"; \GOTO 350
320 PRINT " OCTOBER "; \GOTO 350
330 PRINT " NOVEMBER"; \GOTO 350
340 PRINT " DECEMBER";
350 FOR I=1 TO 18 \ PRINT "*"; \ NEXT I
360 PRINT 365-S;"**";
370 REM  (366-S;      ON LEAP YEARS)
380 PRINT\PRINT\PRINT "    S       M       T       W";
390 PRINT "       T       F       S"
400 PRINT
410 FOR I=1 TO 59 \ PRINT "*"; \ NEXT I
420 REM *********************************
430 FOR W=1 TO 6
440 PRINT\PRINT
450 PRINT TAB(4);
460 REM *********************************
470 FOR G=1 TO 7
480 D=D+1
490 D2=D-S
500 IF D2>M(N) THEN 580
510 IF D2=<0 GOTO 520\PRINT D2;
520 PRINT TAB(4+8*G);
530 NEXT G
540 REM *********************************
550 IF D2=M(N) THEN 590
560 NEXT W
570 REM *********************************
580 D=D-G
590 NEXT N
600 REM *********************************
610 FOR I=1 TO 6\PRINT\NEXT I
620 DATA 0,31,28,31,30,31,30,31,31,30,31,30,31
630 REM  (0,31,29, ..., ON LEAP YEARS)
640 END
