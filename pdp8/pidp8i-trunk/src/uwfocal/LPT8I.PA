/INTERNAL LP08 LPT: HANDLER FOR UWF-4E		-JVZ-

/ THIS HANDLER ALLOWS OUTPUT ON A LINEPRINTER WITHOUT
/SACRIFICING THE OS/8 FILE OUTPUT CAPABILITY FOR THIS
/PURPOSE.   THIS VERSION WILL NOT WORK WITH THE LATER
/KL8E-STYLE INTERFACE SINCE IT TAKES ADVANTAGE OF THE
/'6667' INSTRUCTION TO PREVENT INTERRUPTS.   THE ECHO
/OPTION MAY BE SPECIFIED IN THE USUAL WAY TO CREATE A
/COPY OF THE OUTPUT ON THE TERMINAL.  IT IS ALSO POS-
/SIBLE TO MAKE THE LPT:  THE DEFAULT OUTPUT DEVICE SO
/THAT ERROR MESSAGES, ECHOING FROM AN OS/8 FILE, ETC.
/WILL  APPEAR ON THE  LINEPRINTER  RATHER THAN ON THE
/TERMINAL.   THIS IS SPECIFIED BY THE 'O E'  ('OUTPUT
/EVERYTHING'!)  COMMAND: 'O E LPT:'.   IF YOU SPECIFY
/THE ECHO OPTION IN  -THIS-  COMMAND YOU WILL GET -3-
/COPIES OF THE OUTPUT!  1) ORIGINAL TO THE OS/8 FILE;
/2) A COPY ON THE LPT: AND 3) A COPY ON THE TERMINAL!

	FIELD 1
	*14555		/BEFORE 'FEXP'
LPT,	0		/ENTRY POINT
	6666
	6661		/DONE?
	JMP .-1
	ECKO		/OR '0000'
	CLA CLL
	JMP I LPT
ECKO=	JMS I .
	XOUTL

	*13133
	6667  /OR 6665	/BANISH INTERRUPTS

IFDEF V	 <IFZERO V  <*31;314>>	/12K PATCH TO 'FIRSTV'

	FIELD 0
	*272
LPTDEV,	LPT		/THE ENTRY POINT
DEVICE	LPT		/THE DEVICE NAME
LPTCHK=.
	*304
	CDF 10		/ NOW ADD THE ECHO CHECK:
	ISZ GOSW	/'GOSW' IS SET BY 'GTNAME'
	TAD .+4		/'ECHO' WAS SPECIFIED
	DCA I .+4	/ NO ECHO = 0000
	TAD LPTDEV	/RETURN WITH THE ENTRY POINT
	JMP I LPTCHK
	ECKO		/='JMS I (XOUTL'
	LPT+4		/'LPT:' ECHO LOCATION

/	DEFINITIONS:
	GOSW=36
	XOUTL=3021
	$ = $ = $
