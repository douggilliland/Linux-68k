/UPPER\LOWER CASE SHIFT FOR THE TYPE "" COMMAND		-JVZ-

/ THIS PATCH PROVIDES A WAY FOR UPPER CASE DEVICES TO PRODUCE
/LOWER CASE OUTPUT BY USING A 'SHIFT' CHARACTER (A BACKSLASH)
/TO TOGGLE BETWEEN UPPER/LOWER CASE. EXAMPLE: T "A\B\C" = AbC
/THIS IS USEFUL FOR INSTALLATIONS WITH AN UPPER CASE TERMINAL
/WHO WISH TO MAKE FULL USE OF THE PLOTTER ANNOTATION ROUTINES,
/OR WHO, PERHAPS, HAVE AN UPPER / LOWER CASE LINEPRINTER.  IT
/SACRIFICES THE CR QUOTE MATCH, SO ALL QUOTED STRINGS MUST BE
/TERMINATED PROPERLY.

	FIELD 1
	CHAR=66
	START=177
	AGO=1246
	TQUOT=1310
	TLIST2=1626
	TLIST3=1540
	PRINTC=4534

	*TLIST2+1
	"\		/CHOOSE ANYTHING YOU LIKE
	*TLIST3+1
	TSHIFT		/TOGGLE THE SHIFT SWITCH

	*AGO
	TSTART		/CLEAR THE SHIFT SWITCH

	*TQUOT
	SHIFT		/CHECK FOR A CASE SHIFT
	*TQUOT+7
	JMP I TQUOT	/BEFORE PRINTING THE CHAR

	*3343		/AFTER THE PLOT ROUTINES
TSHIFT,	TAD TSKIP	/TOGGLE THE SHIFT SWITCH
	AND T10
TSTART,	TAD START	/= SKP CLA
	DCA TSKIP
	JMP I QUOTP

T10,	SNL-SKP		/ISOLATE 'SNL' BIT
T40,	"`-"@
M340,	-"`		/= 'SZA'

SHIFT,	TAD CHAR	/GET THE TEXT CHARACTER
	TAD M340	/SEE IF IT IS SHIFTABLE
	STL		/( ONLY SHIFT 300-337 )
	TAD T40
TSKIP,	SKP CLA		/OR 'SNL CLA' AFTER '\'
	TAD T40		/CONVERT TO LOWER CASE
	TAD CHAR
	PRINTC		/PRINT IT
	JMP I QUOTP
QUOTP,	TQUOT+1		/LOOP UNTIL CLOSING '"'
	$
