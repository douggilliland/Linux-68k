/ PATCH TO 'FCOM' TO ADDRESS FIELDS 4-7		-JVZ-

/THIS PATCH MODIFIES THE 'INDEX' ROUTINE SO THAT POSITIVE
/INDICES FROM 0-4095 MAY BE USED TO ADDRESS ALL LOCATIONS
/IN FIELDS 4-7.  THE 'NEGATIVE INDEX' FEATURE HAS BEEN
/ELIMINATED: ONLY FLOATING-POINT STORAGE IS AVAILABLE.

/THE CHANGES ARE CONSTRUCTED SO THAT THEY DO NOT INTER-
/FERE WITH 'FRA' WHICH ALSO USES SOME 'FCOM' ROUTINES.

	XRT=14
	PUT=4410
	GET=4423
	INDEX=4437
	POPA=4521
	ERROR2=4576

	FIELD 0
	*45
	6362		/'32K'

	FIELD 1

	*PUT-1
	NOP		/ELIMINATE THE 'NEG.' RETURN

	*GET
	CDF 40		/USE THIS LOC. FOR A CONSTANT
	JMS INDEX	/AND MOVE THIS DOWN ONE

	*INDEX+1
	POPA		/EXAMINE THE ARGUMENT
	CLL RTL		/MULTIPLY THE INDEX BY 4
	DCA XRT		/LEAVING THE FIELD INFO
	IAC		/IN BIT 11 AND THE LINK
	AND XRT
	RTL		/SHIFT THE FIELD BITS OVER
	RTL
	TAD GET		/ADD THE 'CDF' INSTRUCTION
	DCA .+6		/AND SAVE FOR LATER
	CMA CLL RAL	/=7776
	AND XRT		/CLEAN UP THE INDEX
	CIA
	CMA		/SUBTRACT ONE
	DCA XRT		/AND PUT IT RIGHT BACK
	CDF 40-70
	JMP I INDEX	/DF AND XRT ARE BOTH SET
	$
