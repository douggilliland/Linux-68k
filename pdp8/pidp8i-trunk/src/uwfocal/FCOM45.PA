/ PATCH TO 'FCOM' TO ADDRESS FIELDS 4,5		-JVZ-

/THIS PATCH MODIFIES THE 'INDEX' ROUTINE SO THAT POSITIVE
/INDICES FROM 0-2047 MAY BE USED TO ADDRESS ALL LOCATIONS
/IN FIELDS 4 & 5.  THE 'NEGATIVE INDEX' FEATURE HAS BEEN
/ELIMINATED: ONLY FLOATING-POINT STORAGE IS AVAILABLE.

/THE CHANGES ARE CONSTRUCTED SO THAT THEY DO NOT INTER-
/FERE WITH 'FRA' WHICH ALSO USES SOME 'FCOM' ROUTINES.

	XRT=14
	PUT=4410
	GET=4423
	INDEX=4437
	POPA=4521
	ERROR2=4576

	FIELD 0
	*45
	6264		/'24K'

	FIELD 1

	*PUT-1
	NOP		/ELIMINATE THE 'NEG.' RETURN

	*GET
	CDF 40		/USE THIS LOC. FOR A CONSTANT
	JMS INDEX	/AND MOVE THIS DOWN ONE

	*INDEX+1
	POPA		/EXAMINE THE ARGUMENT
	SPA		/CHECK FOR STORAGE LIMIT
	JMP .+10	/KEEP THE SAME ERROR CODE
	CLL RTL		/MULTIPLY THE INDEX BY 4
	SZA		/LEAVING THE FIELD INFO
	CIA		/IN THE LINK
	CMA		/SUBTRACT ONE
	DCA XRT		/AND SAVE THE INDEX
	RTL		/SHIFT THE FIELD BIT OVER
	SKP
	ERROR2		/INDEX GREATER THAN 2047(10)
	RTL
	TAD GET		/ADD THE 'CDF' INSTRUCTION
	DCA .+1
	CDF 40-50
	JMP I INDEX	/DF AND XRT ARE BOTH SET
	$
